# Maintainer
pkgname=airoscript
pkgver=45.0a122ee
pkgrel=1
epoch=1
pkgdesc="Script to simplify the use of aircrack-ng tools"
arch=('any')
url="http://midnightresearch.com/projects/wicrawl/"
groups=('projectname' 'projectname-*wireless')
license=('GPL')
depends=('mdk3' 'aircrack-ng' 'dhclient' 'perl-yaml-syck')
options=(!makeflags)
makedepends=('git')
source=('git+http://github.com/patrickhno/Airoscript'
        'Makefile.patch'
        'airoscript.desktop')
sha512sums=('SKIP'
            'c7b9eb321243ff436c3bd0887cf11400bedc206c097d6abfc102f4fba4920aa6223b39f978cb64a4c032adb70940c815a4241b7ec4eb017f6b51e0e34fd13bca'
            '0576a704fb93584a12ade376b2b2777c95b96415e510c9233b766ca277b10dcb7a371f7a823ddae93dee45d09a1ec72a32d263e440e0849a23f9a5159b763d31')

pkgver() {
  cd Airoscript
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/Airoscript"
  patch -Np1 < "$srcdir/Makefile.patch"
  find -type f -exec sed -i 's|/sbin|/bin|g' {} \;
}

package() {
  cd "$srcdir/Airoscript"
  install -Dm644 "$srcdir/airoscript.desktop" "$pkgdir/usr/share/applications/airoscript.desktop"
  rm -rf src/locale/*
  make "prefix=$pkgdir/usr" "aircrack_prefix=/usr" "etcdir=$pkgdir/etc/airoscript" install
  # Fix conflict with extra/screen.
  #mv "$pkgdir/etc/screenrc" "$pkgdir/etc/airoscript/screenrc"
  sed -i 's|/local||' "$pkgdir"/etc/airoscript/*
  sed -i 's|/minimal.theme|defaul.theme|' "$pkgdir"/etc/airoscript/*
}
