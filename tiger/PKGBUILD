# Maintainer
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=tiger
pkgver=3.2.3
pkgrel=6
groups=('projectname' 'projectname-*scanners' 'archassault-autonomous')
pkgdesc="A security scanner that checks computer for known problems"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.nongnu.org/${pkgname}/"
license=('GPL')
depends=('make' 'desktop-file-utils' 'systemd')
options=('!emptydirs')
install=${pkgname}.install
backup=("etc/${pkgname}/tigerrc"
        "etc/${pkgname}/cronrc")
source=("http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.tmpfiles.conf"
        "${pkgname}.desktop"
        "${pkgname}.png")
sha512sums=('d0b8326ff0a0159aa99e794f114769c56d520c35a48c16d53d2ca90132ac32614fe84ae661f9ad61aa943ce8171b6051a0495e838e19447e1c34f26d5b730d73'
            '787a0db0d4dd16897a4a0e1f7b5095c27d298a4f0b667a633bbdcd660232c46a3cc2f0c95177cdbb1debb6a2b83d77fbc3fbc2ea2757cdfd3360f2fc2dc1707b'
            '213c0bd9ff98ffbcefa1813f4b1a894cb19a81ce62030af67b815623cec96ad7884764ef99a8473244e8d11ff7d89b13c9765139c6c5e6745cd356e6a1eb3e55'
            '8207fa992a6e66df9e5f454e4375513e60acba5f4289ae641cc2c494cfe70e46293f865a1c25e9af173fb94489367ef6edc48cd294be73d66edf92c34efd1ea3')

prepare() {
    cd ${pkgname}-${pkgver}
    sed -i 's|-m 0444|-Dm0644|' Makefile.in
    sed -i 's|-m 0640 \$\$file \$(DESTDIR)\$(TIGERCONFIG)|-Dm0640 \$\$file \$(DESTDIR)\$(TIGERCONFIG)|' Makefile.in
    sed -i 's/tempfile`/mktemp`/' util/genmsgidx
    sed -i '/explain.idx/s/&&/||/' util/genmsgidx
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
                --mandir=/usr/share/man \
                --with-tigerhome=/usr/share/${pkgname} \
                --with-tigerconfig=/etc/${pkgname} \
                --with-tigerwork=/var/run/${pkgname} \
                --with-tigerlog=/var/log/${pkgname} \
                --with-tigerbin=/usr/bin
    make
}

package() {
    install -Dm644 ${pkgname}.tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/${pkgname}.conf
    install -Dm644 ${pkgname}.desktop "$pkgdir"/usr/share/applications/${pkgname}.desktop
    install -Dm644 ${pkgname}.png "$pkgdir"/usr/share/pixmaps/${pkgname}.png
    cd ${pkgname}-${pkgver}
    install -d "$pkgdir"/usr/share/${pkgname} "$pkgdir"/usr/bin
    make DESTDIR="$pkgdir" install
}
