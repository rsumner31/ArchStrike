# Maintainer <https://projectname.org>
pkgname=enum_shares-git
pkgver=r7.97cba5a
pkgrel=1
pkgdesc="Shared folders enumeration tool"
arch=('any')
url="https://github.com/bwall/HashPump"
license=('GPL2')
groups=('projectname')
depends=('python2')
makedepends=('git')
provides=('enum_shares')
conflicts=('enum_shares')
replaces=('enum_shares')
source=(enum_shares-git::git+https://github.com/dejanlevaja/enum_shares.git)
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  # Use the tag of the last commit
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
   find "${srcdir}" -type f -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package() {
  cd "$srcdir/$pkgname"
  install -dm755 "$pkgdir"/usr/bin
  install -dm755 "$pkgdir"/usr/share/enum_shares-git
  install -m755 enum_shares.py "$pkgdir"/usr/share/enum_shares-git/enum_shares.py
  install -m655 README.md "$pkgdir"/usr/share/enum_shares-git/README.md 
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/enum_shares-git/LICENSE.md
  cat > "$pkgdir"/usr/bin/enum_shares.py <<EOF
#!/bin/sh
cd /usr/share/enum_shares-git
python2 enum_shares.py "\$@"
EOF
  chmod +x "$pkgdir"/usr/bin/enum_shares.py
}
