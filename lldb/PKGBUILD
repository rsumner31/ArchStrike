# Maintainer: ArchAssault <team projectname org>
# Contributor: Nicolas Hureau <archlinux@kalenz.fr>
# Based on lldb-svn PKGBUILD =>
# Contributor: Muhammad Tauqir Ahmad <mtahmed@uwaterloo.ca>
# Contributor: Philipp Sieweck <psi@informatik.uni-kiel.de>
# Contributor: Xavier de Gaye <xdegaye@gmail.com>
# Contributor: Michael Hansen <zrax0111 gmail com>

pkgname=lldb
pkgver=3.5.1
pkgrel=1
pkgdesc="A native debugger Debugger"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA')
depends=('python2' 'libedit' 'clang')
makedepends=('cmake' 'swig')
provides=('lldb')
conflicts=('lldb')
source=(
  "http://llvm.org/releases/${pkgver}/llvm-${pkgver}.src.tar.xz"
  "http://llvm.org/releases/${pkgver}/cfe-${pkgver}.src.tar.xz"
  "http://llvm.org/releases/${pkgver}/lldb-${pkgver}.src.tar.xz"
)
sha512sums=('a643ff173627e813b26077829b1af801b75daa5dcea540c4f05366ed57ef6aeafaf8b3fe4208d4c1b303a048a90ddac48a23488480c165637ae9218de140ae55'
            'bd904586f6155c05d2f7ddd05b86c28feae883cdc60e2cb3b48d4ae0ce55916064bcea414a46518d0fb73618f18a3f3fca6cc4cad81083428a2221299f5c5c7f'
            '4f56af20389d410d22646719d4426411697464a894805ebfb7b1a5a68f22d4d3d384eb6daa58be4dc16246f35e02ecf6f46fb95d90c4fbc047e886b3fd3f0961')

build() {
  cd "$srcdir"
  msg2 "Moving Clang in the LLVM tree ..."
  mv "cfe-${pkgver}.src" "llvm-${pkgver}.src/tools/clang"
  msg2 "Moving LLDB in the LLVM tree ..."
  mv "lldb-${pkgver}.src" "llvm-${pkgver}.src/tools/lldb"

  cd "$srcdir/llvm-${pkgver}.src/tools/lldb"

  msg2 "Applying Archlinux-specific patch ..."

  sed -i -e "s|python-config|python2-config|" lib/Makefile
  sed -i -e "s|python-config|python2-config|" Makefile
  sed -i -e "s|/usr/bin/env python|&2|" scripts/Python/build-swig-Python.sh
  sed -i -e "s|/usr/bin/env python|&2|" scripts/Python/finish-swig-Python-LLDB.sh

  cd "$srcdir/llvm-${pkgver}.src"
  msg2 "Starting build ..."

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  # libffi's includes are in a non-standard location :(
  _libffi_include=$(pkg-config libffi --cflags-only-I | sed 's/-I//')

  export CFLAGS="$CFLAGS -fno-tree-pre"
  export CXXFLAGS="$CXXFLAGS -fno-tree-pre"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLLVM_ENABLE_ASSERTIONS=OFF \
    -DLLVM_ENABLE_FFI=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DFFI_INCLUDE_PATH=$_libffi_include \
    ..

  make
}

package() {
  cd "$srcdir/llvm-${pkgver}.src"

  # Install the license
  install -Dm644 tools/lldb/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$srcdir/llvm-${pkgver}.src/build"

  # Install the lldb binaries
  install -Dm755 bin/lldb "$pkgdir/usr/bin/lldb"
  install -Dm755 bin/lldb-gdbserver "$pkgdir/usr/bin/lldb-gdbserver"

  # Install the lldb library
  install -Dm755 lib/liblldb.so "$pkgdir/usr/lib/liblldb.so"

  # Install the lldb python libraries.
  python_dir="$pkgdir/usr/lib/python2.7/site-packages"
  mkdir -p "$python_dir"
  cp -a lib/python2.7/site-packages/lldb "$python_dir"
  # Fixing python issue
  sed -i 's|python|python2|' $pkgdir/usr/lib/python2.7/site-packages/lldb/utils/symbolication.py

  # Relink the _lldb.so for python
  ln -sf /usr/lib/liblldb.so "$python_dir/lldb/_lldb.so"
}

# vim:set sts=2 sw=2 et:
