# Maintainer
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
pkgname=stegdetect
pkgver=0.6
pkgrel=5
pkgdesc="An automated tool for detecting steganographic content in images"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.outguess.org/"
license=('GPL')
depends=('gtk' 'libevent')
source=("http://www.outguess.org/$pkgname-$pkgver.tar.gz"
        "$pkgname-unified.patch")
sha512sums=('592573b041234f783f33ae245671ca67be53a20218bfd5a65f81f732c42fa436fcb9d6faf71ce7f1100a922c8432f4f99424592017bb6cc306717fe6b17865d7'
            'f541f6f076383787addfac5771f33cf817872f7e85e0cece7689512438f5646d18885c53cc1d6bffb3b7c597f00497cb6ca9ee9f1077906bc53877eebaab970d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "$srcdir/${source[1]##*/}"
}
build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
