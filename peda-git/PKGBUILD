# Maintainer: ArchAssault <team projectname.org>
pkgname=peda-git
pkgver=r63.152e635
pkgrel=1
groups=('projectname' 'projectname-*exploits')
pkgdesc="A Python Exploit Development Assistant for GDB"
arch=('any')
url='https://github.com/longld/peda'
license=('custom')
depends=('python2' 'gdb' 'nasm')
makedepends=('git')
source=("${pkgname}::git+https://github.com/longld/peda.git"
	"peda.py.patch")
install='peda-git.install'
sha512sums=('SKIP'
            '8991761c04c05cec56c01553954f53037f9276a24f7d9e3dad5e0092f08cb683b96c727f7829ad51b3d85ab0a53e6e3a2b805af49517b113ccce838f65346f08')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "${pkgname}" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
  cd "${pkgname}"
  patch -Np1 -i "${srcdir}"/peda.py.patch
}


package() {
  cd "${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -dm755 "${pkgdir}/usr/share/${pkgname}/lib"
  install -Dm644 peda.py "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 lib/* "${pkgdir}/usr/share/${pkgname}/lib/"
  install -Dm644 README "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  #Fixing import error on utils
  mv $pkgdir/usr/share/${pkgname}/lib/utils.py $pkgdir/usr/share/${pkgname}/lib/peda_utils.py
  sed -e 's/^from util/from peda_util/' -i $pkgdir/usr/share/${pkgname}/peda.py $pkgdir/usr/share/${pkgname}/lib/nasm.py $pkgdir/usr/share/${pkgname}/lib/shellcode.py 
}
