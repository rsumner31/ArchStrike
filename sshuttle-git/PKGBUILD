# Maintainer: ArchAssault <team projectname org>
pkgname=sshuttle-git
pkgver=20120810.r198.9ce2fa0
pkgrel=1
epoch=1
pkgdesc="Transparent proxy server that works as a poor man's VPN. Forwards all TCP packets over ssh (and even DNS requests when using --dns option). Doesn't require admin privileges on the server side."
arch=('any')
url="https://github.com/apenwarr/sshuttle"
license=('GPL')
groups=('projectname')
depends=('python2' 'iptables')
makedepends=('git')
provides=('sshuttle')
conflicts=('sshuttle')
source=("git+https://github.com/apenwarr/sshuttle.git"
        "arch-install.patch")
sha512sums=('SKIP'
            'c477928733001de8bbfcbd4d83a9469adafb425c5cfaed59a52d57bea518d6bfdb3a533bc3793a7ed047b947395935fc3cd38fab001cc63fb1dcab3abde0c20d')

pkgver() {
  cd sshuttle
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/sshuttle"

  # Patch launcher with files location (/usr/share/sshuttle)
  patch -p1 -i "$srcdir/arch-install.patch"
}

build() {
  cd "$srcdir/sshuttle/Documentation"
  sed -i 's/python/python2/' md2man.py
  cd ..
  make
}

package() {
  cd "$srcdir/sshuttle"

  #Base Directoires
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/sshuttle"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/sshuttle"

  install -Dm755 $srcdir/sshuttle/sshuttle $pkgdir/usr/bin/sshuttle
 
   
}
