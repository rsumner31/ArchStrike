# Maintainer
pkgname=sploitego
pkgver=r153.d9568dc
pkgrel=1
epoch=1
groups=('projectname' 'projectname-*scanners' 'archassault-fuzzer')
pkgdesc="Maltego Penetration Testing Transforms"
url="https://github.com/allfro/sploitego"
arch=('any')
license=('GPL2')
depends=('python2' 'nmap' 'p0f' 'amap' 'metasploit')
makedepends=('git' 'python2-setuptools')
optdepends=('nessus')
install='sploitego.install'
source=("git+https://github.com/allfro/sploitego.git"
        "sploitego.install")
md5sums=('SKIP'
         '512d661f0405ccaa1a08c41ff95bb863')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build(){
  cd "$srcdir/sploitego"
  python2 setup.py build
}

package() {
  cd "$srcdir/sploitego"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
