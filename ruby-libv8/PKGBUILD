# Maintainer: ArchAssault <team projectname org>
pkgname=ruby-libv8
_gemname=${pkgname#ruby-}
pkgver=3.16.14.3
pkgrel=2
pkgdesc="Ruby gem binary distribution of the V8 JavaScript engine"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/cowboyd/libv8"
license=('MIT')
depends=('ruby' 'rubygems' 'bash' 'python2')
options=(!distcc)
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha256sums=('b5c5fc1803685498b77b1b81817afd4db172c3d730e676cdf6b3ff94eb9232ab')

package() {
  export HOME=/tmp
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  if [[ $CARCH == arm* ]] ; then
    gem install --no-user-install --ignore-dependencies --no-rdoc --no-ri -i "$pkgdir$_gemdir" libv8-$pkgver.gem
  else
    gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" libv8-$pkgver.gem
  fi
}
