# Maintainer: ArchAssault <team projectname org>
pkgname=facebot-git
pkgver=r23.57f6025
pkgrel=2
groups=('projectname' 'projectname-*reconnaissance')
pkgdesc="A facebook profile and reconnaissance system "
arch=('any')
url='https://github.com/pun1sh3r/facebot'
license=('GPL2')
depends=('python2' 'python2-beautifulsoup4' 'python2-configparser' 'python2-facepy' 'python2-binaryornot' 'python2-wsgiref' 'python2-cookiecutter' 'python2-jinja' 'python2-yaml' 
         'python2-multipartposthandler' 'mysql-python' 'python2-gdata' 'python2-markupsafe' 'python2-six' 'python2-pager' 'cookiejar' 'python2-argparse' 'python2-setuptools')
makedepends=('git')
provides=('facebot')
source=("git+https://github.com/pun1sh3r/facebot.git")
sha512sums=('SKIP')

prepare(){
  grep -iRl 'python' "$srcdir/facebot" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


pkgver() {
  cd "$srcdir/facebot"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir/facebot"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/facebot"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/facebot"
  
cat > "$pkgdir/usr/bin/facebot" << EOF
#!/bin/sh
cd /usr/share/facebot
python2 facebotv2.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/facebot"

}
