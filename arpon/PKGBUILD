# Maintainer
pkgname=arpon
pkgver=2.7.2
pkgrel=1
pkgdesc='A portable handler daemon that make ARP protocol secure in order to avoid the Man In The Middle (MITM) attack through ARP Spoofing, ARP Cache Poisoning or ARP Poison Routing (APR) attacks.'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://arpon.sourceforge.net/'
license=('MIT')
depends=('libdnet' 'libnet' 'libpcap')
makedepends=('cmake' 'libdnet')
backup=('etc/conf.d/arpon')
groups=('projectname' 'projectname-*defense')
source=("http://downloads.sourceforge.net/sourceforge/arpon/ArpON-$pkgver.tar.gz"
        "arpon"
        "arpon.conf.d"
        'arpon.desktop'
        'arpon.service')
changelog='CHANGELOG'
sha512sums=('7518f8f8920817725c9b3e0a6420190dcc101b1c0c7cd9eec50870b3f893fe2322eddc90cb71e9c3c35326b4e1730aa02797ca51775caa9401807da3978628b4'
            '0cd0114efd2f35fcf06ec91a56c5aca66adf13a7b25fbf82b2483489d040447d1a3a609f223f911ad1c407b321c6fbc22d32c9200f08fef2bbbf9ec391b7bb90'
            '75526a64be9ab7e6cc787dbff5cdb1e3d7c3b708fc3f9cc1712ea7b16181767aca4747a00c99f4bec6b0de0f86f131c76ec20e2ef7d24c2237c314df95dfc39d'
            '926d46d743a2c45645cf31c8b97533fbac5aaeec6abdb1e1dcaded78a3f0b54409b253584df5f6a5871081cdaf40c91b4246a838836b9e0fdc7754fbd3ec003b'
            'd902e0bc37c922f73ea8c70b388620d86e43e61676ce46011b5c006bef0c09638dd06493e10f911068f0d436439f1da3c365902c386379ddc2a9dfe34c206039')

build() {
  cd "${srcdir}/ArpON-${pkgver}"
  [ -d build ] || mkdir build
  cd build
  cmake -DPCAP_LIB_DIR="/usr/lib" -DPCAP_LIBRARY="/usr/lib/libpcap.so" ..
  make
}

package() {
  cd "${srcdir}/ArpON-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "../LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 "${srcdir}/arpon" "${pkgdir}/etc/rc.d/arpon"
  install -Dm644 "${srcdir}/arpon.conf.d" "${pkgdir}/etc/conf.d/arpon"
  install -Dm644 "${srcdir}/arpon.service" "${pkgdir}/usr/lib/systemd/system/arpon.service"
  install -Dm644 "${srcdir}/arpon.desktop" "${pkgdir}/usr/share/applications/arpon.desktop"
  mv "${pkgdir}"/usr/sbin "${pkgdir}"/usr/bin
  mv "${pkgdir}"/usr/etc/arpon.sarpi "${pkgdir}"/etc/arpon.sarpi
  rm -rf "${pkgdir}"/usr/etc
}
