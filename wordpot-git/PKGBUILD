# Maintainer: ArchAssault <team projectname org>
pkgname=wordpot-git
pkgver=r38.ca12cb5
pkgrel=1
groups=('projectname' 'projectname-*defense')
pkgdesc="A Wordpress Honeypot"
arch=('any')
url='https://github.com/gbrindisi/wordpot'
license=('GPL2')
depends=('python2-flask')
makedepends=('git')
provides=('wordpot')
replaces=('wordpot')
conflicts=('wordpot')
source=("wordpot::git+https://github.com/gbrindisi/wordpot.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/wordpot"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/wordpot" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "$srcdir/wordpot"
  install -dm755 "$pkgdir/usr/share/wordpot/"
  install -dm755 "$pkgdir/usr/bin/"
  cp -a --no-preserve=ownership * "$pkgdir/usr/share/wordpot/"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

cat > "$pkgdir/usr/bin/wordpot" <<EOF
#!/bin/sh
cd /usr/share/wordpot
python2 wordpot.py "\$@"
EOF

chmod +x $pkgdir/usr/bin/wordpot
}
