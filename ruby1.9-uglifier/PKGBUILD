# Maintainer
_gemname=uglifier
pkgname=ruby1.9-$_gemname
pkgver=2.4.0
pkgrel=1
pkgdesc='Ruby wrapper for UglifyJS JavaScript compressor'
arch=(any)
url='http://github.com/lautis/uglifier'
license=(MIT)
depends=(ruby1.9 ruby1.9-execjs)
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha1sums=('73bbdb1e39c757e8f25ce355b9398ebd7d0af19b')

package() {
  local _gemdir="$(ruby-1.9 -e'puts Gem.default_dir')"
  gem-1.9 install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
