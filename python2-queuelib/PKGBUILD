# Maintainer
# Contributor: PyroDevil <p dot devil at gmail dot com>
pkgbase="python-queuelib"
pkgname=("python2-queuelib" "python-queuelib")
pkgver=1.2.2
pkgrel=1
pkgdesc="Collection of persistent (disk-based) queues."
arch=(any)
license=('BSD')
url="http://scrapy.org"
makedepends=('git' 'python' 'python2' 'python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("git://github.com/scrapy/queuelib.git#tag=v${pkgver}")
md5sums=('SKIP')

function _package {
  cd "${srcdir}/queuelib"
  "$1" setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "README.rst" "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}

function package_python2-queuelib {
   _package "python2"
}

function package_python-queuelib {
   _package "python3"
}
