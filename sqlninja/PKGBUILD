# Maintainer: ArchAssault <team projectname org>
pkgname=sqlninja
pkgver=0.2.999
pkgrel=1
groups=('projectname' 'projectname-*webapp' 'archassault-exploit' 'archassault-fuzzer')
pkgdesc="A tool targeted to exploit SQL Injection vulnerabilities on a web application that uses Microsoft SQL Server as its back-end"
arch=('any')
url='http://sqlninja.sourceforge.net/'
license=('GPL2')
depends=('perl' 'perl-netpacket' 'perl-net-pcap' 'perl-net-dns' 'perl-net-rawip' 'perl-io-socket-ssl')
optdepends=('metasploit: to use the metasploit attack mode'
            'tigervnc: a VNC client, vncviewer, to use the VNC payload')
conflicts=('sqlninja-svn')
source=(http://downloads.sourceforge.net/sourceforge/sqlninja/sqlninja-${pkgver}-alpha1.tgz)
sha512sums=('86ec16202098f1e51049f8db6e8567486f2dfb4626c402b03cc9a5f2c43001b3c759cdaac4880886be002a53f44848bc1da896aeaa9194f5050676e24c2ecfa8')

package() {
  cd ${srcdir}/sqlninja-${pkgver}-alpha1

  # Make base directories.
  install -dm755 ${pkgdir}/usr/share/sqlninja
  install -dm755 ${pkgdir}/usr/share/doc/sqlninja
  install -dm755 ${pkgdir}/usr/share/licenses/sqlninja
  install -dm755 ${pkgdir}/usr/bin

  install -m644 ChangeLog README sqlninja-howto.html $pkgdir/usr/share/doc/sqlninja

  cp -pR apps scripts sources sqlninja sqlninja.conf ${pkgdir}/usr/share/sqlninja
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/sqlninja/LICENSE

  # Archlinux fixes
  cd ${pkgdir}/usr/share/sqlninja

  # TODO: Is this still relevant?
  # Check if metasploit-svn or metasploit stable is installed (-svn takes precedence)
  # Apply Archlinux metasploit home directory to sqlninja.conf.
  if [[ -d /usr/src/metasploit ]] ; then
    sed -i 's|/home/sqlninja/trunk/|/usr/src/metasploit|g' sqlninja.conf
  else
    sed -i 's|/home/sqlninja/trunk/|/opt/metasploit|g' sqlninja.conf
  fi

  # Make joint script.
  cat > ${pkgdir}/usr/bin/sqlninja <<EOF
#!/bin/bash
cd /usr/share/sqlninja
./sqlninja "\$@"
EOF
  chmod +x ${pkgdir}/usr/bin/sqlninja
}
