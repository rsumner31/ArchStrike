# Maintainer

_pkgname=google-breakpad
pkgname=${_pkgname}-svn
pkgver=20150210.r1426
pkgrel=1
pkgdesc="An open-source multi-platform crash reporting system"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://code.google.com/p/${_pkgname}"
license=('BSD')
depends=('gcc-libs')
makedepends=('subversion')
options=('staticlibs' '!strip')
source=("${_pkgname}::svn+http://${_pkgname}.googlecode.com/svn/trunk")
sha512sums=('SKIP')

pkgver() {
    cd ${_pkgname}
    printf "%s.r%s" $(svn info | grep "Last Changed Date" | sed 's|Last Changed Date: ||;s|\ .*||;s|-||g') $(svn info | grep Revision: | cut -c11-)
}

build() {
    cd ${_pkgname}
    ./configure --prefix=/usr
    make
}

package() {
    cd ${_pkgname}
    make DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
