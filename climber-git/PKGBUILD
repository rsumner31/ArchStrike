# Maintainer
pkgname=climber-git
pkgver=0.r23.f614304
pkgrel=1
groups=('projectname' 'projectname-*scanner')
pkgdesc="Check UNIX/Linux systems for privilege escalation"
url='https://github.com/raffaele-forte/climber'
arch=('any')
license=('GPL3')
depends=('python2' 'python2-crypto' 'python2-mako' 'python2-paramiko' 'python2-exscript-git')
makedepends=('git')
provides=('climber')
replaces=('climber')
conflicts=('climber')
source=("climber::git+https://github.com/raffaele-forte/climber.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/climber"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/climber" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd $srcdir/climber
  install -dm755 "$pkgdir/usr/share/climber"
  install -dm755 "$pkgdir/usr/bin"

  cp -a --no-preserve=ownership * "$pkgdir/usr/share/climber"

cat > "$pkgdir/usr/bin/climber" <<EOF
#!/bin/sh
cd /usr/share/climber
python2 climber.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/climber"
}
