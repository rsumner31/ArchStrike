# Maintainer
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=bladerf
pkgver=0.14.0
pkgrel=3
pkgdesc="Driver for bladeRF, allowing general purpose software defined radio (SDR)."
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="https://github.com/Nuand/bladeRF"
license=('GPL2')
depends=('libusb')
makedepends=('git' 'cmake')
#backup=("etc/bladerf.cfg")
_git_commit='300356bd50f4d2f0a334bde20d3a5b460a588a04'
_firmware_file='bladeRF_fw_v1.6.1.img'
_fpga_version='v0.0.3'
source=(
        "git://github.com/Nuand/bladeRF#commit=${_git_commit}"
        "http://www.nuand.com/fx3/${_firmware_file}"
        "hostedx40-${_fpga_version}.rbf::http://nuand.com/fpga/${_fpga_version}/hostedx40.rbf"
        "hostedx115-${_fpga_version}.rbf::http://nuand.com/fpga/${_fpga_version}/hostedx115.rbf"
        "88-bladerf.rules")
sha512sums=('SKIP'
            'c69fcaae6f7090748a851764abed67e7dc15f76107c35df654faafcd90eb46d05448474f306bcea685af6c7c479c6ea79a68acfac17b65b30e7520a2de35abb0'
            '36e14ac47491bc2f0d38ebd5d1bf39eaac99e743a460c2b1d84a340299471672dcfda52999f61431bdaf3560ff8bfc0a604f59b93c03cff036c9ac88942efab3'
            '75e3c6a4c7445393c76d250157b49f331b95ea40fe4e028950e6c127005eb7b4c3af7e28ca563563edff5e6ecf092c9101d19f81a955839a4bbfe710afc45b3f'
            '5d448f097203f2ae4764773313b35cde2699f8550522b5a83b12733b507855d9e647e7023502f275b9e2cf0f3d375c352d74a692faf7fcee9c3d394c8a8504f2')
_extract_ver() {
        cat | head -n1 | sed -e "s/[^ ]* *\([0-9]*\))/\1/"
}

pkgver() {
        _MAJOR=`grep VERSION_INFO_MAJOR "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver`
        _MINOR=`grep VERSION_INFO_MINOR "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver`
        _PATCH=`grep VERSION_INFO_PATCH "$srcdir/bladeRF/host/libraries/libbladeRF/CMakeLists.txt" | _extract_ver`
        printf "%s.%s.%s" $_MAJOR $_MINOR $_PATCH
}

build() {
        cd "$srcdir/bladeRF/host"
        mkdir -p build
        cd build
        cmake \
                -DCMAKE_BUILD_TYPE=Release \
                -DINSTALL_UDEV_RULES=OFF \
                -DBUILD_DOCUMENTATION=OFF \
                -DCMAKE_INSTALL_PREFIX=/usr ../
        make
}

package() {
        cd "$srcdir/bladeRF/host/build"
        make DESTDIR=${pkgdir} install
        cd "$srcdir"
        install -vD -m644 88-bladerf.rules                  "${pkgdir}/usr/lib/udev/rules.d/88-bladerf.rules"
        install -vD -m644 "${_firmware_file}"               "${pkgdir}/usr/share/bladerf/firmware/${_firmware_file}"
        install -vD -m644 "hostedx40-${_fpga_version}.rbf"  "${pkgdir}/usr/share/bladerf/fpga/hostedx40.rbf"
        install -vD -m644 "hostedx115-${_fpga_version}.rbf" "${pkgdir}/usr/share/bladerf/fpga/hostedx115.rbf"
}
