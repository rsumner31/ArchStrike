# Maintainer
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

pkgname=tp_smapi-projectname
_pkgname=tp_smapi
pkgver=0.41
pkgrel=55
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('i686' 'x86_64')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
depends=('linux-projectname>=3.17' 'linux-projectname-*<3.18')
makedepends=('linux-projectname-headers>=3.17' 'linux-projectname-*headers<3.18')
install="${pkgname}.install"
source=("https://github.com/downloads/evgeni/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('924a11cdf64bc151d069d4f61ca476cf006773291f363b21a80ce091f560a78c4828d2e522e646a5b133c58149a37cb5c2432a165e92e6980eb510b9488c728e')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  _kernver=${_kernver:-$(uname -r)}
  _extramodules=$(readlink -f "/usr/lib/modules/${_kernver}/extramodules" || true)
  _kernver=$(< "${_extramodules}/version")

  sed -ri -e "s/^(KVER\s*:=).*$/\1 ${_kernver}/" \
	  -e "s#^(KBASE\s*:=).*\$#\1 ${_extramodules%/*}/${_kernver}#" Makefile

  make HDAPS=1
}

package() {
  _kernver=${_kernver:-$(uname -r)}
  _extramodules=$(readlink -f "/usr/lib/modules/${_kernver}/extramodules" || true)
  _kernver=$(< "${_extramodules}/version")

  make -C "${_extramodules%/*}/${_kernver}/build" \
    INSTALL_MOD_PATH="${pkgdir}/${_extramodules%%/lib/*}" \
    M="${srcdir}/${pkgname}-${pkgver}" modules_install

  cd "${pkgdir}/${_extramodules%/*}"
  mv "${_kernver}/extra" "${_extramodules##*/}"
  rmdir "${_kernver}"

  # compress kernel modules
  find "${pkgdir}" -name "*.ko" -exec gzip -9 {} +

  # load module on startup
  echo tp_smapi > "${srcdir}/${_pkgname}.conf"
  install -Dm644 "${srcdir}/${_pkgname}.conf" "${pkgdir}/usr/lib/modules-load.d/${pkgname}.conf"

}
