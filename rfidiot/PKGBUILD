# Maintainer
pkgname=rfidiot
_gitname=RFIDIOt
pkgver=82ef9e0
pkgrel=1
epoch=1
groups=('projectname' 'projectname-*wireless')
pkgdesc="An open source python library for exploring RFID devices."
url='http://rfidiot.org'
license=('GPL')
makedepends=('git' 'python2-setuptools' 'python2-crypto')
depends=('python2' 'openssl' 'python2-pyscard')
arch=('any')
source=('git+https://github.com/AdamLaurie/RFIDIOt')
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --always | sed 's|-|.|'
}

prepare() {
  cd "$srcdir/$_gitname"
  # Change shebang lines.
  find . -type f -name '*.py' -exec sed -i '1s/python\r\?$/python2/' '{}' \;
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  cd "$pkgdir/usr/bin"

  # Remove test scripts.
  rm -f *.sh

  # Strip extensions and add the 'rfidiot-' prefix.
  for file in *.py ; do
    # Remove extension. Add prefix.
    new_file=rfidiot-$(basename "${file%.*}")
    # '_' -> '-'
    new_file=${new_file/_/-}
    install -Dm755 "$file" "$new_file"
    rm -f "$file"
  done
}
