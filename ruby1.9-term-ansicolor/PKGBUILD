# Maintainer
_gemname=term-ansicolor
pkgname=ruby1.9-$_gemname
pkgver=1.2.2
pkgrel=3
pkgdesc='Ruby library that colors strings using ANSI escape sequences'
arch=(any)
url='http://flori.github.com/term-ansicolor'
license=(GPL-2)
depends=(ruby1.9 ruby1.9-tins)
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha1sums=('51b378f762b86f4e221fc483dab4d05e7cb1090b')

package() {
  local _gemdir="$(ruby-1.9 -e'puts Gem.default_dir')"
  gem-1.9 install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  mv "$pkgdir"/usr/bin/colortab "$pkgdir"/usr/bin/colortab-1.9
  mv "$pkgdir"/usr/bin/decolor "$pkgdir"/usr/bin/decolor-1.9
  mv "$pkgdir"/usr/bin/term_display "$pkgdir"/usr/bin/term_display-1.9
  mv "$pkgdir"/usr/bin/term_mandel "$pkgdir"/usr/bin/term_mandel-1.9
  rm "$pkgdir"/usr/bin/cdiff
}
